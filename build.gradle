buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:$safe_args_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.14.2")
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

detekt {
    toolVersion = "1.14.2"
    input = files("$projectDir")
    config = files("$project.projectDir/config/detekt.yml")
    reports {
        xml {
            enabled = true
            destination = file("$project.projectDir/detekt-report.xml")
        }
        html {
            enabled = true
            destination = file("$project.projectDir/detekt-report.html")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task installGitHook(type: Copy) {
    def lintingConfigScript = new File(rootProject.rootDir, '.git/hooks/pre-commit')
    if (!lintingConfigScript.exists()) {
        from new File(rootProject.rootDir, '.githooks/pre-commit')
        into { new File(rootProject.rootDir, '.git/hooks') }
        fileMode 0777
    }
}

tasks.getByPath('app:preBuild').dependsOn installGitHook